use v6.d;
use Test;
use RakuDoc::Render;

plan 9;

my $ast;
my $rv;
my RakuDoc::Processor $rdp .= new( :test );

$ast = Q:to/QAST/.AST;
    =begin rakudoc
    =config head2 :!toc
    =head2 Third test text
    =end rakudoc
    QAST
$rv = $rdp.render( $ast, :process );
like $rv.body.Str, /'<head>' .+ 'level' .+ 2 .+ '</head>' /, 'plain head has level 2';
like $rv.body.Str, / '<head>' .+ 'toc' .+ 'False' .+ '</head>' /, 'head got toc data';
$ast = Q:to/QAST/.AST;
    =head2 First header
    =begin section
    =head2 Second header without option
    =config head2 :!toc
    =head2 Third test text with option
    =end section
    =head2 Fourth without option
    QAST
$rv = $rdp.render( $ast, :process );
my regex hd { '<head>' $<cont> = (.+?) '</head>' }
my $parse = $rv.body.Str ~~ / [<hd> .*? ]+ $/;
is $parse<hd>.elems, 4, 'got 4 heads';
like $parse<hd>[0]<cont>, / 'contents' .+ 'First header' /, 'Got first head';
unlike $parse<hd>[0]<cont>, / 'toc' /, 'First head has no toc';
like $parse<hd>[1]<cont>, /'contents' .+ 'Second header' /, 'got 2nd head';
unlike $parse<hd>[1]<cont>, /'toc' /, '2nd head has not got toc';
like $parse<hd>[2]<cont>, /'contents' .+ 'Third test text' .+ 'toc' /, '3rd head has toc';
unlike $parse<hd>[3]<cont>, / 'toc' /, '3rd head has no toc';

done-testing;
