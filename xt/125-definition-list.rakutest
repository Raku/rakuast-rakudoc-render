use v6.d;
use Test;
use RakuDoc::Render;

plan *;

my $ast;
my $rv;
my RakuDoc::Processor $rdp .= new( :test );
$ast = Q:to/QAST/.AST;
    =begin rakudoc
    =defn Happy
    When you're not blue.

    =defn Blue
    When you're not happy.
    =end rakudoc
    QAST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    '<defn-list>' .+
    '<defn>' .+
    'contents:' .+ "When you're not blue" .+
    'target:' .+ 'defn_Happy' .*
    'term:' .+ 'Happy' .*
    '</defn>' .*
    '<defn>' .+
    'contents:' .+ "When you're not happy" .+
    'target:' .+ 'defn_Blue' .+
    'term:' .+ 'Blue' .+
    '</defn>' .+
    '</defn-list>'
    /, 'Basic definitions';

$ast = Q:to/QAST/.AST;
    =begin rakudoc
    =defn Happy
    When you're B<not> blue.

    =defn Blue
    When B<you're> not happy.
    =end rakudoc
    QAST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    '<defn-list>' .*
    '<defn>' .*
    'contents:' .* "When you're " .* '<basis>' .* 'not' .* '</basis>' .* 'blue' .*
    'target:' .* 'defn_Happy' .*
    'term:' .* 'Happy' .*
    '</defn>' .*
    '<defn>' .*
    'contents:' .* 'When ' .* '<basis>' .* "you're" .* '</basis>' .* ' not happy' .*
    'target:' .* 'defn_Blue' .*
    'term:' .* 'Blue' .*
    '</defn>' .*
    '</defn-list>'
    /, 'Expositions have embedded markup';
$ast = Q:to/QAST/.AST;
    =begin rakudoc
    =defn lexiphania
    An unfortunate proclivity for
    employing grandiloquisms (for example, words such as "proclivity", "grandiloquism", and indeed "lexiphania").

    =defn glossoligation
    Restraint of the tongue (voluntary or otherwise)

    To treat his chronic L<defn:lexiphania> the doctor prescribed an immediate L<defn:glossoligation>
    or, if that proved ineffective, a complete cephalectomy.
    =end rakudoc
    QAST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    '<defn>' .+? 'target:' .+? 'defn_lexiphania' .+?
    '<defn>' .+? 'target:' .+? 'defn_glossoligation' .+
    '<link>' .*?
        'link-label:' .+? 'lexiphania' .+
        'place:' .+? '｢An unfortunate proclivity' .+? 'indeed "lexiphania"). ｣' .+
        'target:' .+? 'defn_lexiphania' .+
        'type:' .+? 'defn' .+
    '<link>' .*?
        'link-label:' .+? 'glossoligation' .+
        'place:' .+? '｢Restraint of' .+? 'otherwise) ｣' .+
        'target:' .+? 'defn_glossoligation' .+
        'type:' .+? 'defn'
/, 'links to defns work';
$ast = Q:to/QAST/.AST;
    =begin rakudoc

    To treat his chronic L<defn:lexiphania> the doctor prescribed an immediate L<defn:glossoligation>
    or, if that proved ineffective, a complete cephalectomy.

    =defn lexiphania
    An unfortunate proclivity for
    employing grandiloquisms (for example, words such as "proclivity", "grandiloquism", and indeed "lexiphania").

    =defn glossoligation
    Restraint of the tongue (voluntary or otherwise)
    =end rakudoc
    QAST
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    'To treat his' .+?
    '<link>' .*?
        'link-label:' .+? 'lexiphania' .+
        'place:' .+? '｢An unfortunate proclivity' .+? 'indeed "lexiphania"). ｣' .+
        'target:' .+? 'defn_lexiphania' .+
        'type:' .+? 'defn' .+
    '</link>' .*?
    '<link>' .*?
        'link-label:' .+? 'glossoligation' .+
        'place:' .+? '｢Restraint of' .+? 'otherwise) ｣' .+
        'target:' .+? 'defn_glossoligation' .+
        'type:' .+? 'defn' .+
    '</link>' .+
    '<defn>' .+? 'target:' .+? 'defn_lexiphania' .+?
    '<defn>' .+? 'target:' .+? 'defn_glossoligation' .+
/, 'links to forward defns work';
$ast = Q:to/QAST/.AST;
    =begin rakudoc
    There ensued a terrible moment of D<coyotus interruptus>: a brief
    suspension of the effects of gravity.

    As the canyon edge loomed, a memory of L<defn:coyotus interruptus> passed him by.
    =end rakudoc
    QAST

say $ast;
$rv = $rdp.render( $ast, :pre-finalised );
like $rv.body.Str, /
    'There ensued a terrible moment of ' .*?
    '<markup-D>' .+?
    'contents' .+? 'coyotus interruptus' .+?
    'target' .+? 'inline_defn_coyotus_interuptus' .+?
    '</markup-D>'
/, 'inline definition rendered';
like $rv.body.Str, /
    '<link>' .+?
    'contents' .+? 'coyotus interuptus' .+?
    'target' .+? 'inline_defn_coyotus_interruptus' .?
    'rendered-value' .+? 'There ensued' .+? '</markup-D' .+?
    '</link>'
/, 'link picks up the inline-definition';

done-testing;
