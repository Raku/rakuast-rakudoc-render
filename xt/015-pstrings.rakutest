use v6.d;
use Test;
use RakuDoc::Templates;

my $com-channel = Supplier::Preserving.new;
my $payload;

plan 35;

#tests of PCell
my PCell $pc .= new(:$com-channel, :id<FFF>);
nok $pc.is-expanded, 'PCell not expanded yet';
is $pc.Str, '｢FFF UNAVAILABLE｣', 'stringifies with an unavailable message';
like $pc.debug, / 'Waiting for: FFF' /,'debug string with id';
$payload = 'some stuff';
$com-channel.emit(%(:$payload, :id<FFF>));
like $pc.debug, / 'Expanded to:' /,'Expanding changes debug str';
is $pc.Str, 'some stuff', 'stringifies with payload';
ok $pc.is-expanded, 'PCell now expanded';

## tests of PStr
my PStr $s .= new;
lives-ok { $s ~= PCell.new(:$com-channel, :id<AAA>) }, 'right concatenation of PCell lives';
is $s.Str, '｢AAA UNAVAILABLE｣', 'PCell stringifies with UNAVAILABLE';
ok $s.has-PCells, 'finds PCells in string';
lives-ok { $s ~= "Some stuff " }, 'right concatenation of Str lives';
$s ~= PCell.new(:$com-channel, :id<BBB>);
is $s.Str, '｢AAA UNAVAILABLE｣Some stuff ｢BBB UNAVAILABLE｣', 'still UNAVAILABLE';
$payload = 'second value first';
$com-channel.emit(%(:$payload, :id<BBB>));
like $s.Str, / 'AAA' .+ $payload /, 'AAA is still unavailable, payload now in stringification';
lives-ok { sink '<start tab>' ~ $s }, 'left concatenation works';
like $s.Str, / 'start tab' .+ 'AAA' .+ $payload /, 'string added to start';
like $s.debug, /
    'PCell'
    .+ 'Waiting for: AAA'
    /, 'debug method works';
$payload = 'first value now';
$com-channel.emit(%(:$payload, :id<AAA>));
unlike $s.debug, / 'PCell, Waiting' /, 'debug shows no waiting';
unlike $s.Str, / AAA /, 'AAA UNKNOWN removed';
like $s.Str, / $payload /, 'replaced  by payload';
sink '<begin>' ~ $s ~ '<end>';
like $s.Str, /  '<begin>' .+ '<end>' $ /, 'multiple concatenations';
nok $s.has-PCells, 'strip removes all expanded PCells';
my $rv = $s.Str;
is $rv, '<begin><start tab>first value nowSome stuff second value first<end>', '$s now just a string';
is $s.lead, $rv, 'lead same as whole string';
is $s.tail, $rv, 'tail same as whole string';

$s .= new;
$s ~= 'start' ~ PCell.new(:$com-channel, :id('AAA'));
like $s.Str, / 'start' .+ 'AAA'/, 'concatenation with starting Str';
sink 'more at front ' ~ $s;
is $s.lead, 'more at front start', 'concatenated front strings';
$s ~= ' with stuff at end' ~ ' <end>';
is $s.tail, ' with stuff at end <end>', 'concatenated final strings';

lives-ok { $s .= new: ' one ', PCell.new(:$com-channel, :id('AAA')), ' two '},
        'creates a new Pstr from a list';
$rv = $s;
like $s.Str, / ' one ' .+ 'AAA' .+ ' two ' /, 'expected PStr';
$s.trim-trailing;
like $s.Str, / ' one ' .+ 'AAA' .+ ' two' $ /, 'should trim trailing space';
$s.trim-leading;
like $s.Str, / ^ 'one ' .+ 'AAA' .+ ' two' $ /, 'should trim leading space';
is $s.segments, 3, 'trimming should not change segments';
$rv.trim;
$rv.elems.say;
like $rv.Str, / ^ 'one ' .+ 'AAA' .+ ' two' $ /, 'should trim leading/trailing space';
is $rv.segments, 3, 'trim should not change segments';
$s .= new( '  just a single string ');
is $s.segments, 1, 'simple string is one segment';
$s.trim;
like $s.Str, / ^ 'just a single string' $ /, 'trims start/end space without repeating';
done-testing;
